																		DAX MEASURES - CRIMES
Total Crimes = COUNTROWS(Crimes)

Total Crimes Card = 
	VAR Million = 1000000
	VAR OneThousand = 1000
	VAR TenThousand = 10000
	VAR HundredThousand = 100000
	VAR FormattedTotalCrimes =
		SWITCH(
			TRUE(),
			[Total Crimes] >= Million, FORMAT(DIVIDE([Total Crimes],Million),"#.0\M"),
			[Total Crimes] >= HundredThousand && [Total Crimes] < Million, FORMAT(DIVIDE([Total Crimes], OneThousand),"#\K"),
			[Total Crimes] >= TenThousand && [Total Crimes] < Million, FORMAT(ROUND(DIVIDE([Total Crimes],OneThousand),1),"#.0\K"),
			[Total Crimes] >= OneThousand && [Total Crimes] < HundredThousand, FORMAT(ROUND(DIVIDE([Total Crimes],OneThousand),2),"#.00\K"),
			FORMAT([Total Crimes], "General Number")
		)
	RETURN FormattedTotalCrimes

Total Crimes Prev. Year = 
	CALCULATE(
		[Total Crimes],
		DATEADD(
			'Calendar Table'[Date],
			-1,
			YEAR
		)
	)
	
Total Crimes Top N Areas = 
	VAR RankAreas =
		RANKX(
			ALLSELECTED('District Lookup'[Area]),
			[Total Crimes],
			,
			DESC,
			Dense
	)
	VAR RankAreas_2 =
		IF(
			HASONEVALUE('District Lookup'[Area]),
			RankAreas,
			BLANK()
	)
	VAR TopNTotalCrimes =
		IF(
			RankAreas_2 <= SELECTEDVALUE('pTopNAreas'[pTopNAreas_Series]),
			[Total Crimes],
			BLANK()
		)
	RETURN TopNTotalCrimes

Total Crimes % = 
	VAR AllSelectedCrimes =
		CALCULATE(
			[Total Crimes],
			ALLSELECTED(Crimes)
		)
	VAR Output =
		DIVIDE(
			[Total Crimes],
			AllSelectedCrimes
		)
	RETURN Output

Total Crimes % Change = 
	VAR CrimeRatio =
		DIVIDE(
			[Total Crimes],
			[Total Crimes Prev. Year]
		)
	VAR TotalCrimesPctChange = 
		IF(
			ISBLANK([Total Crimes Prev. Year]),
			BLANK(),
			CrimeRatio - 1
		)
	RETURN TotalCrimesPctChange
	
Most Common Crime = 
VAR TopCrime = 
    UPPER(
        TOPN(
            1,
            VALUES('Crime Lookup'[Crime Description]),
            [Total Crimes],
            DESC
        )
    )
VAR TopCrimeFix =
    IF(
        TopCrime IN {"Intimate Partner - Simple Assault","Intimate Partner - Aggravated Assault"},
        "Intimate Partner - Assault",
        TopCrime
    )
RETURN TopCrimeFix
	
Top Clusters Frequency = 
	VAR AllCrimeClusters = -- Total Crimes regardless of the cluster and the slicer (to be used as denominator for both returned measures
		CALCULATE(
			[Total Crimes],
			REMOVEFILTERS('Crime Lookup'[Crime Cluster]),
			REMOVEFILTERS('pTopNCrimes'[pTopNCrimes_Series])
		)

	-- Create the measure to be returned when the filter context for Crime Cluster is unique
	VAR ClusterRank = -- Crime clusters ranked by total crimes
		RANKX(
			ALLSELECTED('Crime Lookup'[Crime Cluster]),
			[Total Crimes],
			,
			DESC
		)
	VAR ClusterFrequency = -- Total Crimes ratio for each cluster
		DIVIDE(
			[Total Crimes],
			AllCrimeClusters
		)
	VAR ClusterTotalCrimes = -- Dynamic selection of the top clusters based on the slicer
		IF(
			ClusterRank <= SELECTEDVALUE('pTopNCrimes'[pTopNCrimes_Series]),
			ClusterFrequency,
			BLANK()
		)

	-- Create the measure to be returned when the filter context for Crime Cluster holds multiple values
	VAR TopNTotalCrimes = -- Total Crimes for all crimes in the top selected clusters 
		CALCULATE(
			[Total Crimes],
			TOPN(
				SELECTEDVALUE('pTopNCrimes'[pTopNCrimes_Series]),
				VALUES('Crime Lookup'[Crime Cluster]),
				[Total Crimes],
				DESC
			)
		)
	VAR TopClustersFrequency = -- Total Crimes ratio
		DIVIDE(
			TopNTotalCrimes,
			--[Total Crimes],
			AllCrimeClusters
		)

	-- When the Crime Clusters in the filter context is unique ClusterTotalCrimes is returned, TopClustersFrequency otherwise
	VAR Result = 
		IF(
			HASONEVALUE('Crime Lookup'[Crime Cluster]),
			ClusterTotalCrimes,
			TopClustersFrequency
		)
	RETURN Result